plugins {
    id 'java'
    id 'maven-publish'
    id "com.github.johnrengelman.shadow" version "7.1.2"
    id 'net.kyori.blossom' version "1.2.0"
    id "io.freefair.lombok" version "8.0.1"
}

repositories {
    maven { url = uri('https://repo.fantasyrealms.net/other-snapshots') }
    maven { url = uri('https://repo.fantasyrealms.net/other-libraries') }
    maven { url = uri('https://repo.fantasyrealms.net/releases') }
    maven { url = uri('https://repo.extendedclip.com/content/repositories/placeholderapi/') }
    maven { url = uri('https://repo.glaremasters.me/repository/concuncan/') }
    maven { url = uri('https://repo.maven.apache.org/maven2/') }
    maven { url = uri('https://jitpack.io') }

    mavenCentral()
    mavenLocal()
}

def versionOnly = project.getProperty("version")
def versionWithGit = versionOnly + "-${getGitHash()}"

group = 'cc.happyareabean'
version = versionWithGit
description = 'SWMHook'

String getGitHash() {
    def output = new ByteArrayOutputStream()
    exec {
        standardOutput = output
        commandLine 'git', 'rev-parse', '--short', 'HEAD'
    }
    return output.toString().trim()
}

String getDate() {
    def date = new Date()
    def formattedDate = date.format('yyyy.MM.dd')
    return formattedDate
}

dependencies {
    implementation 'de.exlll:configlib-core:2.2.1'

    implementation 'com.github.Revxrsal.Lamp:common:3.1.2'
    implementation 'com.github.Revxrsal.Lamp:bukkit:3.1.2'
    implementation 'net.kyori:adventure-api:4.12.0'
    implementation 'net.kyori:adventure-platform-bukkit:4.2.0'
    implementation 'org.bstats:bstats-bukkit:3.0.0'
    implementation 'org.semver4j:semver4j:4.3.0'

    compileOnly 'org.spigotmc:spigot-api:1.8.8-R0.1-SNAPSHOT'
    compileOnly 'org.spigotmc:spigot:1.8.8-R0.1-SNAPSHOT'

    compileOnly 'me.clip:placeholderapi:2.10.10'
    compileOnly 'com.grinderwolf:slimeworldmanager-api:2.2.1'
    compileOnly 'com.grinderwolf:slimeworldmanager-plugin:2.2.1'
    compileOnly "rip.diamond:Eden:1.1.4-1f8eb9a"
}

blossom {
    def constants = "src/main/java/cc/happyareabean/swmhook/constants/Constants.java"
    replaceToken("\${pluginVersion}", version.toString(), constants)
    replaceToken("\${rawVersion}", versionOnly.toString(), constants)
    replaceToken("\${commit}", getGitHash().toString(), constants)
    replaceToken("\${buildDate}", getDate().toString(), constants)
}

tasks.processResources {
    expand("pluginVersion": project.version, "commit": getGitHash(), "buildDate": getDate())
}

import com.github.jengelman.gradle.plugins.shadow.tasks.ConfigureShadowRelocation
shadowJar {
    archiveClassifier.set('')
    archiveVersion.set('')
}

tasks.register('relocateShadowJar', ConfigureShadowRelocation) {
    target = tasks.shadowJar
    prefix = "cc.happyareabean.swmhook.libs"
}

tasks.shadowJar.dependsOn tasks.relocateShadowJar

tasks.register('javadocJar', Jar) {
    archiveClassifier.set("javadoc")
    archiveVersion.set((String) versionOnly)
    from javadoc
}

build {
    finalizedBy {
        tasks.shadowJar
    }
}

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    withJavadocJar()
}

compileJava { // Preserve parameter names in the bytecode
    options.compilerArgs += ["-parameters"]
    options.fork = true
    options.forkOptions.executable = "javac"
    options.encoding = 'UTF-8'
}

javadoc {
    options.encoding = 'UTF-8'
}

publishing {
    repositories {
        maven {
            name = "frsReleases"
            url = "https://repo.fantasyrealms.net/releases/"
            credentials {
                username findProperty("frsRepositoryUsername").toString()
                password findProperty("frsRepositoryPassword").toString()
            }
        }
    }
    publications {
        maven(MavenPublication) { publication ->
            project.shadow.component(publication)
            artifact javadocJar

            version = versionOnly
        }
    }
}
